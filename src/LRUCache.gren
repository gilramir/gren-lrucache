module LRUCache exposing (LRUCache, newLRUCache, get, put, remove, toDict)

import Dict exposing (Dict)

import LRUCache.LinkedList as LinkedList exposing (LinkedList)

type LRUCache comparable v
    = LRUCache
        { maxSize : Int
        , ordering: LinkedList comparable v
        }

{-| Create the LRUCache, setting the maximum size.
-}
newLRUCache: Int -> LRUCache comparable v
newLRUCache maxSize =
    LRUCache
        { maxSize = maxSize
        , ordering = LinkedList.emptyLinkedList
        }

{-| Get a dict that represents this cache.
-}
toDict : LRUCache comparable v -> Dict comparable v
toDict (LRUCache { ordering }) =
    LinkedList.toDict ordering

{-| Inserts, or update, a key/value pair.
-}
put: comparable -> v -> LRUCache comparable v -> LRUCache comparable v
put key value (LRUCache {maxSize, ordering} as cache) =
    let
        updatedOrdering =
            LinkedList.pushFront key value ordering
        (LinkedList.LinkedList { nodes }) = updatedOrdering
    in
        if (Dict.count nodes <= maxSize) then
            LRUCache
            { maxSize = maxSize
            , ordering = updatedOrdering
            }
        else
            let
                maybeLruKey = LinkedList.getLastKey ordering
            in
                when maybeLruKey is
                    Nothing ->
                        -- This is impossible, because we would
                        -- have hit this when the node count <= maxSize
                        cache

                    Just lruKey ->
                        let
                            orderingWithoutLru =
                                LinkedList.remove lruKey updatedOrdering
                        in
                            LRUCache
                            { maxSize = maxSize
                            , ordering = orderingWithoutLru
                            }


{-| Maybe get a value for the given key.
-}
get : comparable -> LRUCache comparable v -> { cache: LRUCache comparable v, value: Maybe v }
get key (LRUCache { maxSize, ordering} as cache) =
    let
        (LinkedList.LinkedList { nodes }) = ordering
    in
        when Dict.get key nodes is
            Nothing ->
                { cache = cache, value = Nothing }

            Just (LinkedList.Link {dictValue}) ->
                { cache = LRUCache
                    { maxSize = maxSize
                    , ordering = LinkedList.pushFront key dictValue ordering
                    },
                    value = Just dictValue
                }

{-| Remove a key from the cache.  If the key doesn't exist, no harm is done.
-}
remove : comparable -> LRUCache comparable v -> LRUCache comparable v
remove key (LRUCache {maxSize, ordering}) =
    LRUCache
        { ordering = LinkedList.remove key ordering
        , maxSize = maxSize
        }
